buildscript {
    ext.kotlin_version = "1.1.3-2"

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
    }
}

apply plugin: "application"
apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "maven"
apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"

mainClassName = "org.rimumarkup.RimucKt"
applicationName = "rimu-kt"
version = "0.5.0"

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile group: "junit", name: "junit", version: "4.12"
    testCompile "com.github.stefanbirkner:system-rules:1.16.1"
    testCompile "com.beust:klaxon:0.31"
}

startScripts {
    // Set name of executable start scripts in binary distribution.
    applicationName = "rimuc-kt"
}

// Create a sources JAR file when the project is built.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    // TODO: Exclude resource files from source archive.
    from sourceSets.main.allSource
}

// Add sources JAR to archives.
artifacts {
    archives sourcesJar
}

// Exclude distributions from uploaded archives.
configurations.archives.with {
    artifacts.remove artifacts.find { it.archiveTask.is distTar }
    artifacts.remove artifacts.find { it.archiveTask.is distZip }
}

// Publish this project to local Maven repository with uploadArchives task.
group = "org.rimumarkup"
uploadArchives {
    repositories {
        mavenDeployer {
            repository url: "file:./maven"
        }
    }
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java
            artifact sourcesJar
//            groupId project.group
//            artifactId project.applicationName
//            version project.version
        }
    }
}

Properties localProperties = new Properties()
localProperties.load(new FileInputStream("./local.properties"))

// Used by bintrayUpload task publishes Maven library to Bintray.
// See https://github.com/bintray/gradle-bintray-plugin
bintray {
    dryRun = false
    publish = false
    override = false
    user = localProperties['bintray.user']
    key = localProperties['bintray.apikey']
    publications = ['library']
    pkg {
        repo = 'maven'
        name = applicationName
        licenses = ['MIT']
        vcsUrl = 'https://github.com/srackham/rimu-kt'
        websiteUrl = 'http://rimumarkup.org'
        issueTrackerUrl = 'https://github.com/srackham/rimu/issues'
        desc = 'Rimu Markup for the JVM.'
        labels = ['rimu', 'markup', 'asciidoc', 'markdown']
        publicDownloadNumbers = false
        version {
            name = project.version
        }
    }
}

